plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.15.3'
}

group 'pl.tfij'
archivesBaseName = "result"
version 'v1.1.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        sonatype(MavenPublication) {
            artifactId = 'result'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Functional resul'
                description = 'Simple two layer cache.'
                url = 'https://github.com/tfij/result'
                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }
                developers {
                    developer {
                        id = 'tfij'
                        name = 'Tomasz Fija≈Çkowski'
                        email = 'tomasz@chi.pl'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/tfij/result.git'
                    developerConnection = 'scm:git:https://github.com/tfij/result.git'
                    url = 'https://github.com/tfij/result.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = (version.toString().endsWith('SNAPSHOT')) ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('SONATYPE_USERNAME')
                password = System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

if (!(System.getenv('SIGNING_GPG_KEY_ID') as String)?.isEmpty()) {
    signing {
        useInMemoryPgpKeys(
                System.getenv('SIGNING_GPG_KEY_ID') as String,
                System.getenv('SIGNING_GPG_PRIVATE_KEY') as String,
                System.getenv('SIGNING_GPG_PRIVATE_KEY_PASSWORD') as String
        )
        sign publishing.publications.sonatype
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
